apply plugin: 'java-library'
apply plugin: 'signing'
apply plugin: 'checkstyle'
apply plugin: 'com.github.spotbugs'

group = 'com.transferwise.common'

apply from: "${project.rootDir}/build.libraries.gradle"

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
    withJavadocJar()
}

jar {
    baseName projectArtifactName
    manifest {
        attributes(
                "Implementation-Title": projectArtifactName,
                "Implementation-Version": archiveVersion
        )
    }
}

dependencies {
    annotationProcessor platform(libraries.springBootDependencies)
    implementation platform(libraries.springBootDependencies)
    compileOnly platform(libraries.springBootDependencies)
    testAnnotationProcessor platform(libraries.springBootDependencies)

    annotationProcessor libraries.lombok
    annotationProcessor libraries.springBootConfigurationProcessor
    testAnnotationProcessor libraries.lombok

    implementation libraries.lombok

    compileOnly libraries.spotbugsAnnotations
}

compileJava {
    options.encoding = 'utf-8'
    options.compilerArgs << '-parameters'
}

repositories {
    mavenCentral()
    maven {
        url 'https://arti.tw.ee/artifactory/libs-release'
    }
    mavenLocal()
}

configurations {
    all {
        exclude module: 'slf4j-log4j12'
    }
}

test {
    useJUnitPlatform()
    
    testLogging {
        events "passed", "skipped", "failed", "standardError"
        showStackTraces = true
        exceptionFormat = 'full'
    }
}

tasks.withType(Checkstyle) {
    config = resources.text.fromFile(file('../google_checks.xml'))

    maxWarnings = 0

    reports {
        xml.enabled false
        html.enabled true
    }
}

tasks.findAll { it.name.startsWith("spotbugs") }*.configure {
    effort = "max"

    reports {
        xml.enabled false
        html.enabled true
    }
}
