apply plugin: 'java-library'
apply plugin: 'signing'
apply plugin: 'checkstyle'
apply plugin: 'com.github.spotbugs'

group = 'com.transferwise.common'

apply from: "${project.rootDir}/build.libraries.gradle"

configurations {
    all {
        exclude module: 'slf4j-log4j12'

        resolutionStrategy {
            failOnVersionConflict()
            preferProjectModules()
            if (System.getenv("RUNS_IN_CI") == "true") {
                // This is faster, than using Gradle's `--refresh-dependencies`, which will refresh also all non-dynamic things.
                cacheDynamicVersionsFor 10, 'minutes'
                cacheChangingModulesFor 10, 'minutes'
            }
        }
    }

    local {
        canBeResolved(false)
        canBeConsumed(false)
    }
    compileClasspath {
        extendsFrom(local)
    }
    runtimeClasspath {
        extendsFrom(local)
    }
    testCompileClasspath {
        extendsFrom(local)
    }
    testRuntimeClasspath {
        extendsFrom(local)
    }
    annotationProcessor {
        extendsFrom(local)
    }
    testAnnotationProcessor {
        extendsFrom(local)
    }
}

java {
    if (springBootVersion.startsWith("3.")) {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    } else {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    withSourcesJar()
    withJavadocJar()
}

jar {
    manifest {
        attributes(
                "Implementation-Title": "Wise graceful shutdown",
                "Implementation-Version": archiveVersion
        )
    }
}

dependencies {
    local platform(libraries.springBootDependencies)
    local libraries.lombok
    compileOnly libraries.spotbugsAnnotations
}

compileJava {
    options.encoding = 'utf-8'
    options.compilerArgs << '-parameters'
}

repositories {
    mavenCentral()
    maven {
        url 'https://arti.tw.ee/artifactory/libs-release'
    }
    mavenLocal()
}

test {
    useJUnitPlatform()
}

tasks.withType(Checkstyle) {
    config = resources.text.fromFile(file('../google_checks.xml'))
    maxWarnings = 0

    reports {
        xml.required = true
        html.required = true
    }
}

tasks.findAll { it.name.startsWith("spotbugs") }*.configure {
    effort = "max"
    excludeFilter = file('../spotbugs-exclude.xml')

    reports {
        xml.required = true
        html.required = true
    }
}
